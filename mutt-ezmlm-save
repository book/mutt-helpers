#!/usr/bin/env perl
use 5.024;
use warnings;
use Getopt::Long;
use File::Spec;
use Sys::Hostname;
use Email::MIME;
use Config::INI::Reader;

my %option = ( config => File::Spec->catfile( $ENV{HOME}, '.ezmlm' ), );

GetOptions( \%option, 'config=s' )
  or die "Usage: mutt-ezmlm-save [ --config <config-file> ]\n";

my $ezmlm   = Config::INI::Reader->read_file( $option{config} );
my $maildir = $ezmlm->{_}{maildir}
  // die "No maildir defined in $option{config}";

# create email from STDIN
my $email = Email::MIME->new(
    do { local $/; <<>> }
);

my ($list) =
  $email->header_str('Subject') =~
  /\A(\S+) Digest of: get.[0-9]+(?:_[0-9]+)?\z/;

my $config = $ezmlm->{$list}   // die "Unknown list $list\n";
my $folder = $config->{folder} // die "No folder defined for $list";

my @parts = $email->subparts;
shift @parts;    # drop the explanatory message

my $i = 1;          # per process counter
my $h = hostname;
for my $part (@parts) {

    # grab the digest number from the metadata
    my $attached = Email::MIME->new( $part->body );
    my ($digest) = $part->filename =~ m{\A${list}_([0-9]+)\.ezm\z};
    die "No digest found" unless $digest;

    # update useful mailing-list headers
    $attached->header_str_set( 'List-Id' => $config->{list_id} )
      if $config->{list_id};
    $attached->header_str_set( 'X-List-Archive' => sprintf( $config->{list_archive}, $digest ) )
      if $config->{list_archive};

    # build the uniq Maildir email
    my $file = sprintf "%d.%d_%d.%s", time, $$, $i++, $h;

    # create the mail in tmp/
    my $tmp = File::Spec->catfile( $maildir, $folder, 'tmp', $file );
    open my $fh, '>', $tmp or die "Can't open $tmp for saving\n";
    print $fh $attached->as_string;
    close $fh;

    # move the mail to new/
    my $new = File::Spec->catfile( $maildir, $folder, 'new', $file );
    rename $tmp, $new or die "Couldn't rename $tmp to $new: $!";

    say sprintf 'Created %s in %s with message %s', $file, $folder,
      $attached->header_str('Message-Id');
}
