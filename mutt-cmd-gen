#!/usr/bin/env perl
use strict;
use warnings;
use Email::MIME;

my %pattern = (    # ways to pick an address and a pattern
    wide => [
        [ 'list-id', qr/(<.*>)/, '~hlist-id:.*%s' ],
        [ 'from',    qr/<(.*)>/, '~f%s|~C%s' ],
        [ 'from',    qr/(.*)/,   '~f%s|~C%s' ],        # fallback
    ],
);

# pick up command and pattern
my $cmd = shift || 'limit';
my $pat = shift || 'wide';

die "Unknown pattern type `$pat`\n" if !exists $pattern{$pat};
my @patterns = @{ $pattern{$pat} };

# create email from STDIN
my $email = Email::MIME->new( do { local $/; <>; } );

# pick up the address
my ( $addr, $fmt );
for (@patterns) {
    ( my $header, my $re, $fmt ) = @$_;
    ($addr) = ( lc $email->header($header) || '' ) =~ $re;
    last if $addr;
}

# spit out the requested Mutt command
if ($addr) {
    $addr =~ s/([](.+)[])/\\\\\\\\$1/g;    # escape what needs to be
    $addr =~ s/ +/.*/g;
    my $expr = sprintf $fmt, ($addr) x ( $fmt =~ y/%// );
    printf qq{"\\<$cmd\\>%s\\<enter\\>"}, $expr;
}

__END__

# Usage:

# pick up a temporary file when starting Mutt
set my_tmp_file=`mktemp --tmpdir mutt-XXXXXXXX`

# tag-pattern
macro index,pager ,t "<enter-command> set wait_key=no<enter>\
<pipe-message>mutt-cmd-gen tag-pattern > $my_tmp_file<enter>\
<enter-command> push \`cat $my_tmp_file\`<enter>\
<enter-command> set wait_key=yes<enter>" "Magically limit to all related messages"

# limit
macro index,pager ,l "<enter-command> set wait_key=no<enter>\
<pipe-message>mutt-cmd-gen limit > $my_tmp_file<enter>\
<enter-command> push \`cat $my_tmp_file\`<enter>\
<enter-command> set wait_key=yes<enter>" "Magically limit to all related messages"
